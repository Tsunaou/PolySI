plugins {
    id 'application'
    id "io.freefair.lombok" version '6.3.0'
}

group = 'CobraVerifier'
version = '0.0.1-SNAPSHOT'
description = 'CobraVerifier'

application {
    mainClass = 'Launcher'
}

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'info.picocli:picocli:4.6.3'
    implementation files('build/monosat/monosat.jar')

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

test {
    jvmArgs += [ "-Xmx4096m" ]
    useJUnitPlatform()
}

tasks.register('buildMonoSAT') {
    exec {
        commandLine 'cmake', '-B', "$buildDir/monosat", '-S', "$projectDir/monosat", '-DJAVA=ON', '-DBUILD_STATIC=OFF'
    }

    exec {
        commandLine 'cmake', '--build', "$buildDir/monosat", '-j8'
    }
}

tasks.register('buildCUDA') {
    exec {
        commandLine 'mkdir', '-p', "$buildDir/gpu"
    }

    exec {
        commandLine 'nvcc',
            '-Xcompiler', '-fPIC', '-shared', '-std=c++11', "-O3",
            "-I${System.properties.'java.home'}/include",
            "-I${System.properties.'java.home'}/include/linux",
            '-o', "$buildDir/gpu/libgpumm.so",
            "$projectDir/src/main/cuda/gpu_GPUmm.cu",
            "-lcuda", "-lcudart", "-lcublas", "-lcusparse"
    }
}

jar {
    dependsOn 'buildMonoSAT', 'buildCUDA'

    manifest {
        attributes 'Main-Class': 'Launcher'
    }

    duplicatesStrategy('exclude')
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from {
        "$buildDir/monosat/libmonosat.so"
    }

    from {
        "$buildDir/gpu/libgpumm.so"
    }
}
